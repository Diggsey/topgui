
function guiHScrollbar(id as guiId, x, y, w, h, value, maxValue, pageSize, smallStep)
    guiBeginControl(x, y, w, h, 1)
    
    value = guiHScrollbarLogic(id, w, h, value, maxValue, pageSize, smallStep)
    guiHScrollbarDraw(id, w, h, value, maxValue, pageSize)
    
    guiEndControl()
endfunction value

function guiVScrollbar(id as guiId, x, y, w, h, value, maxValue, pageSize, smallStep)
    guiBeginControl(x, y, w, h, 1)
    
    value = guiVScrollbarLogic(id, w, h, value, maxValue, pageSize, smallStep)
    guiVScrollbarDraw(id, w, h, value, maxValue, pageSize)
    
    guiEndControl()
endfunction value

function guiHScrollbarLogic(id, w, h, value, maxValue, pageSize, smallStep)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    ` If mouse over, try becoming hot
    if guiInClip(gui.msx, gui.msy)
        guiSetHot(id)
    endif
    
    ` If disabled
    if maxValue <= 0
        exitfunction 0
    endif
    
    inc value, guiGetScroll(id, id)*smallStep
    
    slideSize = (w-h*2)
    barSize = (pageSize*slideSize)/(maxValue+pageSize)
    barPos = ((slideSize-barSize)*value)/maxValue
    
    inButton1 = guiInRect(gui.msx, gui.msy, x, y, x+h, y+h)
    inButton2 = guiInRect(gui.msx, gui.msy, x+w-h, y, x+w, y+h)
    inBar = guiInRect(gui.msx, gui.msy, x+h+barPos, y, x+h+barPos+barSize, y+h)
    inSlider = (inButton1 = 0) and (inButton2 = 0) and (inBar = 0)
    inSlider1 = inSlider and (gui.msx <= x+h+barPos)
    inSlider2 = inSlider and (gui.msx > x+h+barPos)
    
    if gui.msd = 1
        if guiGetHot() = id
            
            if guiSetActive(id)
                if inBar
                    gui.dragMode = 1
                    gui.dragX = barPos - gui.msx
                else
                    guiSetDelay(id, 500)
                    
                    gui.dragMode = 0
                    if inButton1 then gui.dragMode = 2 : dec value, smallStep
                    if inButton2 then gui.dragMode = 3 : inc value, smallStep
                    if inSlider1 then gui.dragMode = 4 : dec value, pageSize
                    if inSlider2 then gui.dragMode = 5 : inc value, pageSize
                endif
            endif
        endif
    endif
    
    if guiGetActive() = id
        if gui.msc = 1
            guiStayActive()
            
            select gui.dragMode
                case 1
                    barPos = gui.dragX + gui.msx
                    value = (barPos*maxValue)/(slideSize-barSize)
                endcase
                case 2
                    if inButton1
                        if guiHasDelayed() then guiSetDelay(id, 50) : dec value, smallStep
                    endif
                endcase
                case 3
                    if inButton2
                        if guiHasDelayed() then guiSetDelay(id, 50) : inc value, smallStep
                    endif
                endcase
                case 4
                    if inSlider1
                        if guiHasDelayed() then guiSetDelay(id, 50) : dec value, pageSize
                    endif
                endcase
                case 5
                    if inSlider2
                        if guiHasDelayed() then guiSetDelay(id, 50) : inc value, pageSize
                    endif
                endcase
            endselect
        endif
    endif
    
    value = clamp(value, 0, maxValue)
    
endfunction value

function guiVScrollbarLogic(id, w, h, value, maxValue, pageSize, smallStep)
    x = guiAdjustX(0)
    y = guiAdjustY(0)

    ` If mouse over, try becoming hot
    if guiInClip(gui.msx, gui.msy)
        guiSetHot(id)
    endif
    
    ` If disabled
    if maxValue <= 0
        exitfunction 0
    endif

    inc value, guiGetScroll(id, id)*smallStep
    
    slideSize = (h-w*2)
    barSize = (pageSize*slideSize)/(maxValue+pageSize)
    barPos = ((slideSize-barSize)*value)/maxValue
    
    inButton1 = guiInRect(gui.msx, gui.msy, x, y, x+w, y+w)
    inButton2 = guiInRect(gui.msx, gui.msy, x, y+h-w, x+w, y+h)
    inBar = guiInRect(gui.msx, gui.msy, x, y+w+barPos, x+w, y+w+barPos+barSize)
    inSlider = (inButton1 = 0) and (inButton2 = 0) and (inBar = 0)
    inSlider1 = inSlider and (gui.msy <= y+w+barPos)
    inSlider2 = inSlider and (gui.msy > y+w+barPos)
    
    if gui.msd = 1
        if guiGetHot() = id
            
            if guiSetActive(id)
                if inBar
                    gui.dragMode = 1
                    gui.dragY = barPos - gui.msy
                else
                    guiSetDelay(id, 500)
                    
                    gui.dragMode = 0
                    if inButton1 then gui.dragMode = 2 : dec value, smallStep
                    if inButton2 then gui.dragMode = 3 : inc value, smallStep
                    if inSlider1 then gui.dragMode = 4 : dec value, pageSize
                    if inSlider2 then gui.dragMode = 5 : inc value, pageSize
                endif
            endif
        endif
    endif
    
    if guiGetActive() = id
        if gui.msc = 1
            guiStayActive()
            
            select gui.dragMode
                case 1
                    barPos = gui.dragY + gui.msy
                    value = (barPos*maxValue)/(slideSize-barSize)
                endcase
                case 2
                    if inButton1
                        if guiHasDelayed() then guiSetDelay(id, 50) : dec value, smallStep
                    endif
                endcase
                case 3
                    if inButton2
                        if guiHasDelayed() then guiSetDelay(id, 50) : inc value, smallStep
                    endif
                endcase
                case 4
                    if inSlider1
                        if guiHasDelayed() then guiSetDelay(id, 50) : dec value, pageSize
                    endif
                endcase
                case 5
                    if inSlider2
                        if guiHasDelayed() then guiSetDelay(id, 50) : inc value, pageSize
                    endif
                endcase
            endselect
        endif
    endif
    
    value = clamp(value, 0, maxValue)
    
endfunction value

function guiHScrollbarDraw(id, w, h, value, maxValue, pageSize)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    disabled = maxValue <= 0
    
    a2FillBox x, y, x+w, y+h, 0xFF808080, 0xFF808080, 0xFFC0C0C0, 0xFFC0C0C0
    
    if disabled = 0
        slideSize = (w-h*2)
        barSize = (pageSize*slideSize)/(maxValue+pageSize)
        barPos = ((slideSize-barSize)*value)/maxValue
        
        if guiGetHot() = id
            a2FillBox x, y, x+h, y+h, 0xFFFFFF80, 0xFFFFFF80, 0xFFFFC040, 0xFFFFC040
            a2FillBox x+w-h, y, x+w, y+h, 0xFFFFFF80, 0xFFFFFF80, 0xFFFFC040, 0xFFFFC040
            
            a2FillBox x+h+barPos, y, x+h+barPos+barSize, y+h, 0xFFFFFF80, 0xFFFFFF80, 0xFFFFC040, 0xFFFFC040
        else
            a2FillBox x, y, x+h, y+h, 0xFFC0C0C0, 0xFFC0C0C0, 0xFF808080, 0xFF808080
            a2FillBox x+w-h, y, x+w, y+h, 0xFFC0C0C0, 0xFFC0C0C0, 0xFF808080, 0xFF808080
            
            a2FillBox x+h+barPos, y, x+h+barPos+barSize, y+h, 0xFFC0C0C0, 0xFFC0C0C0, 0xFF808080, 0xFF808080
        endif
        
        a2FillTriangle x+7, y+h/2, x+h-9, y+5, x+h-9, y+h-6, 0xFF000000
        a2FillTriangle x+w-8, y+h/2, x+w+8-h, y+5, x+w+8-h, y+h-6, 0xFF000000
        a2Triangle x+7, y+h/2, x+h-9, y+5, x+h-9, y+h-6, 0xFF000000
        a2Triangle x+w-8, y+h/2, x+w+8-h, y+5, x+w+8-h, y+h-6, 0xFF000000
        
        a2SetLineAA 0
        a2Box x, y, x+h-1, y+h-1, 0xFF000000
        a2Box x+w-h, y, x+w-1, y+h-1, 0xFF000000
        
        a2Box x+h+barPos, y, x+h+barPos+barSize-1, y+h-1, 0xFF000000
        a2SetLineAA 1
    else
        a2FillTriangle x+7, y+h/2, x+h-9, y+5, x+h-9, y+h-6, 0x40000000
        a2FillTriangle x+w-8, y+h/2, x+w+8-h, y+5, x+w+8-h, y+h-6, 0x40000000
        a2Triangle x+7, y+h/2, x+h-9, y+5, x+h-9, y+h-6, 0x40000000
        a2Triangle x+w-8, y+h/2, x+w+8-h, y+5, x+w+8-h, y+h-6, 0x40000000
        
        a2SetLineAA 0
        a2Box x, y, x+h-1, y+h-1, 0x40000000
        a2Box x+w-h, y, x+w-1, y+h-1, 0x40000000
        a2SetLineAA 1
    endif
    
    
endfunction

function guiVScrollbarDraw(id, w, h, value, maxValue, pageSize)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    disabled = maxValue <= 0
    
    a2FillBox x, y, x+w, y+h, 0xFF808080, 0xFFC0C0C0, 0xFFC0C0C0, 0xFF808080
    
    if disabled = 0
        slideSize = (h-w*2)
        barSize = (pageSize*slideSize)/(maxValue+pageSize)
        barPos = ((slideSize-barSize)*value)/maxValue
        
        if guiGetHot() = id
            a2FillBox x, y, x+w, y+w, 0xFFFFFF80, 0xFFFFC040, 0xFFFFC040, 0xFFFFFF80
            a2FillBox x, y+h-w, x+w, y+h, 0xFFFFFF80, 0xFFFFC040, 0xFFFFC040, 0xFFFFFF80
            
            a2FillBox x, y+w+barPos, x+w, y+w+barPos+barSize, 0xFFFFFF80, 0xFFFFC040, 0xFFFFC040, 0xFFFFFF80
        else
            a2FillBox x, y, x+w, y+w, 0xFFC0C0C0, 0xFF808080, 0xFF808080, 0xFFC0C0C0
            a2FillBox x, y+h-w, x+w, y+h, 0xFFC0C0C0, 0xFF808080, 0xFF808080, 0xFFC0C0C0
            
            a2FillBox x, y+w+barPos, x+w, y+w+barPos+barSize, 0xFFC0C0C0, 0xFF808080, 0xFF808080, 0xFFC0C0C0
        endif
        
        a2FillTriangle x+w/2, y+7, x+5, y+w-9, x+w-6, y+w-9, 0xFF000000
        a2FillTriangle x+w/2, y+h-8, x+5, y+h+8-w, x+w-6, y+h+8-w, 0xFF000000
        a2Triangle x+w/2, y+7, x+5, y+w-9, x+w-6, y+w-9, 0xFF000000
        a2Triangle x+w/2, y+h-8, x+5, y+h+8-w, x+w-6, y+h+8-w, 0xFF000000
        
        a2SetLineAA 0
        a2Box x, y, x+w-1, y+w-1, 0xFF000000
        a2Box x, y+h-w, x+w-1, y+h-1, 0xFF000000
        
        a2Box x, y+w+barPos, x+w-1, y+w+barPos+barSize-1, 0xFF000000
        a2SetLineAA 1
    else
        a2FillTriangle x+w/2, y+7, x+5, y+w-9, x+w-6, y+w-9, 0x40000000
        a2FillTriangle x+w/2, y+h-8, x+5, y+h+8-w, x+w-6, y+h+8-w, 0x40000000
        a2Triangle x+w/2, y+7, x+5, y+w-9, x+w-6, y+w-9, 0x40000000
        a2Triangle x+w/2, y+h-8, x+5, y+h+8-w, x+w-6, y+h+8-w, 0x40000000
        
        a2SetLineAA 0
        a2Box x, y, x+w-1, y+w-1, 0x40000000
        a2Box x, y+h-w, x+w-1, y+h-1, 0x40000000
        a2SetLineAA 1
    endif

endfunction
