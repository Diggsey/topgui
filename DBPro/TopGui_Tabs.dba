
function guiTabsBegin(id as guiId, x, y, w, h, tabHeight, index)
    guiLayout(id, x, y, w, h)
    
    guiBeginControl(x, y, w, h, 1)
    
    guiTabsBeginLogic(id)
    
    guiPushInteger(guiAdjustX(0))
    guiPushInteger(tabHeight)
    guiPushInteger(0)
    guiPushInteger(index)
    guiPushInteger(w)
    guiPushInteger(h)
    guiPushInteger(id)
    guiPushInteger(index)
endfunction

function guiTabsBeginLogic(id as guiId)
    focused = guiFocus(id, 1)
    
    ` If mouse over, try becoming hot
    if guiInClip(guiMouseX(), guiMouseY())
        guiSetHot(id)
    endif
    
    if guiGetHot() = id
        if gui.msd = 1
            guiSetFocus(id, 0, 0)
        endif
    endif
    
endfunction

function guiTabBegin(caption$, width)
    newIndex = guiPopInteger()
    id = guiPopInteger()
    h = guiPopInteger()
    w = guiPopInteger()
    index = guiPopInteger()
    tabIndex = guiPopInteger()
    tabHeight = guiPopInteger()
    tabX = guiPopInteger()
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    if width = 0 then width = a2GetTextWidth(gui.fontId, caption$)+12
    
    tabHot = (guiGetHot() = id) and guiInRect(guiMouseX(), guiMouseY(), tabX, y, tabX+width, y+tabHeight)
    selected = index = tabIndex
    
    newIndex = guiTabLogic(id, index, newIndex, tabIndex, tabHot)
    guiTabDraw(id, index, tabIndex, tabX, width, tabHeight, w, h, caption$, tabHot)
    
    guiPushInteger(tabX+width)
    guiPushInteger(tabHeight)
    guiPushInteger(tabIndex+1)
    guiPushInteger(index)
    guiPushInteger(w)
    guiPushInteger(h)
    guiPushInteger(id)
    guiPushInteger(newIndex)
    
    if selected then guiBeginContainer(id, 4, tabHeight+3, w-8, h-tabHeight-7, 0, 0)
endfunction selected

function guiTabLogic(id as guiId, index, newIndex, tabIndex, tabHot)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    if index <> tabIndex
        if tabHot
            if gui.msd = 1
                newIndex = tabIndex
                gui.selStart = index
            endif
        endif
    endif
endfunction newIndex

function guiTabDraw(id as guiId, index, tabIndex, tabX, width, tabHeight, w, h, caption$, tabHot)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    if index = tabIndex
        a2FillBox tabX, y, tabX+width, y+tabHeight, THEME_ACTIVE_LIGHT, THEME_ACTIVE_LIGHT, THEME_ACTIVE_DARK, THEME_ACTIVE_DARK
        
        a2SetLineAA 0
        a2Line tabX, y+tabHeight-1, tabX, y, THEME_OUTLINE_DARK
        a2Line tabX, y, tabX+width-1, y, THEME_OUTLINE_DARK
        a2Line tabX+width-1, y, tabX+width-1, y+tabHeight, THEME_OUTLINE_DARK
        a2Line x, y+tabHeight-1, tabX, y+tabHeight-1, THEME_OUTLINE_DARK
        a2Line tabX+width, y+tabHeight-1, x+w, y+tabHeight-1, THEME_OUTLINE_DARK
        
        a2Line x+w-1, y+tabHeight-1, x+w-1, y+h-1, THEME_OUTLINE_DARK
        a2Line x+w-1, y+h-1, x, y+h-1, THEME_OUTLINE_DARK
        a2Line x, y+h-1, x, y+tabHeight-1, THEME_OUTLINE_DARK
        
        a2Box x+1, y+tabHeight, x+w-2, y+h-2, THEME_ACTIVE_DARK
        a2Box x+2, y+tabHeight+1, x+w-3, y+h-3, THEME_ACTIVE_DARK
        a2Box x+3, y+tabHeight+2, x+w-4, y+h-4, THEME_OUTLINE_DARK
        a2SetLineAA 1
        
        `a2BoxText gui.fontId, tabX, y, tabX+width, y+tabHeight, caption$, 1, 1, 1, THEME_TEXT_DARK
        guiFastBoxText(gui.fontId, tabX, y, tabX+width, y+tabHeight, caption$, 1, 1, 1, THEME_TEXT_DARK)
        
        if guiGetFocus() = id
            guiDrawFocusRect(tabX+3, y+3, tabX+width-4, y+tabHeight-4, THEME_FOCUS_DARK)
        endif
    else
        if tabHot
            a2FillBox tabX, y, tabX+width, y+tabHeight, guiFadeColor(THEME_ACTIVE_LIGHT, 0x80), guiFadeColor(THEME_ACTIVE_LIGHT, 0x80), guiFadeColor(THEME_ACTIVE_DARK, 0x80), guiFadeColor(THEME_ACTIVE_DARK, 0x80)
            
            a2SetLineAA 0
            a2Line tabX, y+tabHeight-1, tabX, y, guiFadeColor(THEME_OUTLINE_DARK, 0x80)
            a2Line tabX, y, tabX+width-1, y, guiFadeColor(THEME_OUTLINE_DARK, 0x80)
            a2Line tabX+width-1, y, tabX+width-1, y+tabHeight, guiFadeColor(THEME_OUTLINE_DARK, 0x80)
            a2SetLineAA 1
        endif
    
        `a2BoxText gui.fontId, tabX, y, tabX+width, y+tabHeight, caption$, 1, 1, 1, THEME_TEXT_LIGHT
        guiFastBoxText(gui.fontId, tabX, y, tabX+width, y+tabHeight, caption$, 1, 1, 1, THEME_TEXT_LIGHT)
    endif
endfunction

function guiTabEnd()
    guiEndContainer()
endfunction

function guiTabsEnd()
    newIndex = guiPopInteger()
    id = guiPopInteger()
    h = guiPopInteger()
    w = guiPopInteger()
    index = guiPopInteger()
    tabNum = guiPopInteger()
    tabHeight = guiPopInteger()
    tabX = guiPopInteger()
    
    newIndex = guiTabsEndLogic(id, newIndex, tabNum)

    guiEndControl()
endfunction newIndex

function guiTabsEndLogic(id as guiId, newIndex, tabNum)
    if guiGetFocus() = id
        if guiProcessKeyDown(id, VK_LEFT, 0) then dec newIndex
        if guiProcessKeyDown(id, VK_RIGHT, 0) then inc newIndex
    endif
    newIndex = clamp(newIndex, 0, tabNum-1)
endfunction newIndex
