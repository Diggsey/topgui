
function guiWindowBegin(id as guiId, x, y, w, h, caption$, child, focused)
    guiBeginControl(x, y, w, h, child)
    guiGetScroll(id, id)
    
    activated = guiWindowLogic(id, w, h)
    guiWindowDrawBackground(id, w, h)
    guiWindowDraw(id, w, h, caption$, focused)
    
    guiBeginContainer(id, 4, 26, w-8, h-30, 0, 0)
endfunction activated

function guiWindowEnd()
    guiEndContainer()
    
    guiEndControl()
endfunction

function guiScrollWindowBegin(id as guiId, x, y, w, h, caption$, scrollx, scrolly, scrollw, scrollh, child, focused)
    guiBeginControl(x, y, w, h, child)
    
    activated = guiScrollWindowLogic(id, w, h)
    guiScrollWindowDraw(id, w, h, caption$, scrollx, scrolly, scrollw, scrollh, focused)
    
    guiBeginContainer(id, 4, 26, w-28, h-50, scrollx, scrolly)
endfunction activated

function guiScrollWindowEnd()
    guiEndContainer()
    
    guiEndControl()
endfunction

function guiScrollWindowLogic(id, w, h)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    activated = guiWindowLogic(id, w, h)
    
    if guiGetHot() = id
        if gui.msd = 1
            if guiInRect(gui.msx, gui.msy, x+w-24, y+h-24, x+w, y+h)
                if guiSetActive(id)
                    gui.dragMode = (1 << 3) or (1 << 4)
                    gui.dragX = w - gui.msx
                    gui.dragY = h - gui.msy
                endif
            endif
        endif
    endif
    
endfunction activated

function guiScrollWindowDraw(id, w, h, caption$, scrollx, scrolly, scrollw, scrollh, focused)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    guiWindowDrawBackground(id, w, h)
    
    pageSizeH = w-29
    pageSizeV = h-51
    
    horzId = CHILD_ID(id)
    vertId = CHILD_ID(id)
    
    guiGetScroll(id, vertId)
    gui.interaction.scrollX = guiHScrollbar(horzId, 3, h-24, w-26, 21, scrollx, scrollw-pageSizeH, pageSizeH, 25)
    gui.interaction.scrollY = guiVScrollbar(vertId, w-24, 25, 21, h-48, scrolly, scrollh-pageSizeV, pageSizeV, 25)
    
    guiWindowDraw(id, w, h, caption$, focused)
    
    if focused
        a2FillBox x+w-24, y+h-24, x+w-1, y+h-1, 0xFFFFC040
    else
        a2FillBox x+w-24, y+h-24, x+w-1, y+h-1, 0xFF808080
    endif
    
    a2SetLineAA 0
    a2Line x+w-24, y+h-24, x+w-24, y+h-3, 0xFF000000
    a2Line x+w-24, y+h-24, x+w-3, y+h-24, 0xFF000000
    a2SetLineAA 1
    
    a2Line x+w-5, y+h-4, x+w-4, y+h-5, 0x80FFFFFF
    a2Line x+w-7, y+h-4, x+w-4, y+h-7, 0x80000000
    a2Line x+w-10, y+h-4, x+w-4, y+h-10, 0x80FFFFFF
    a2Line x+w-12, y+h-4, x+w-4, y+h-12, 0x80000000
    a2Line x+w-15, y+h-4, x+w-4, y+h-15, 0x80FFFFFF
    a2Line x+w-17, y+h-4, x+w-4, y+h-17, 0x80000000
endfunction

function guiWindowLogic(id as guiId, w, h)
    activated = 0

    ` Convert 0, 0 to screen coordinates
    x = guiAdjustX(0)
    y = guiAdjustY(0)

    ` If mouse over, try becoming hot
    if guiInClip(gui.msx, gui.msy)
        guiSetHot(id)
    endif
    
    if gui.msd
        if guiGetHot() = id or guiIsChildHot(id)
            activated = 1
            
            if guiIsChildFocused(id) = 0
                guiSetFocus(0)
            endif
        endif
    endif
    
    if guiFocusChanged()
        if guiIsChildFocused(id)
            activated = 1
        endif
    endif
    
    if guiGetHot() = id
        if gui.msd = 1
            ` If in titlebar
            if guiInRect(gui.msx, gui.msy, x+4, y+4, x+w-4, y+25)
                if guiSetActive(id)
                    gui.dragMode = 1
                    gui.dragX = x - gui.msx
                    gui.dragY = y - gui.msy
                endif
            else
                ` Left border
                lb = guiInRect(gui.msx, gui.msy, x, y, x+4, y+h)
                ` Top border
                tb = guiInRect(gui.msx, gui.msy, x, y, x+w, y+4)
                ` Right border
                rb = guiInRect(gui.msx, gui.msy, x+w-4, y, x+w, y+h)
                ` Bottom border
                bb = guiInRect(gui.msx, gui.msy, x, y+h-4, x+w, y+h)
                
                if lb or tb or rb or bb
                    gui.dragMode = (lb << 1) or (tb << 2) or (rb << 3) or (bb << 4)
                    if guiSetActive(id)
                        if lb
                            gui.dragX = x - gui.msx
                        else
                            if rb
                                gui.dragX = w - gui.msx
                            endif
                        endif
                        
                        if tb
                            gui.dragY = y - gui.msy
                        else
                            if bb
                                gui.dragY = h - gui.msy
                            endif
                        endif
                    endif
                endif
            endif
            
        endif
    endif
    
    if guiGetActive() = id
        if gui.msc = 1
            guiStayActive()
            if gui.dragMode = 1
                newx = gui.dragX + gui.msx
                newy = gui.dragY + gui.msy
                
                gui.interaction.deltaX = newx-x
                gui.interaction.deltaY = newy-y
            endif
            if (gui.dragMode and 2)
                newx = gui.dragX + gui.msx
                
                gui.interaction.deltaX = newx-x
                gui.interaction.deltaW = -gui.interaction.deltaX
            endif
            if (gui.dragMode and 4)
                newy = gui.dragY + gui.msy
                
                gui.interaction.deltaY = newy-y
                gui.interaction.deltaH = -gui.interaction.deltaY
            endif
            if (gui.dragMode and 8)
                neww = gui.dragX + gui.msx
                
                gui.interaction.deltaW = neww-w
            endif
            if (gui.dragMode and 16)
                newh = gui.dragY + gui.msy
                
                gui.interaction.deltaH = newh-h
            endif
        endif
    endif
endfunction activated

function guiWindowListPtrs(arrayptr as integer, focused)
    windowCount = get arrayptr count(arrayptr)
    windowFocus = -1
    
    for i = 0 to windowCount
        ptr = peek integer(get arrayptr item ptr(arrayptr, i))
        if call function ptr(ptr, (i = windowCount) and focused)
            windowFocus = i
        endif
    next i

    if windowFocus <> -1
        rotate arrayptr arrayptr, windowFocus, windowCount
    endif
endfunction

function guiWindowListNames(arrayptr as integer, focused)
    windowCount = get arrayptr count(arrayptr)
    windowFocus = -1
    
    for i = 0 to windowCount
        name$ = peek string(peek integer(get arrayptr item ptr(arrayptr, i)))
        if call function name(name$, (i = windowCount) and focused)
            windowFocus = i
        endif
    next i

    if windowFocus <> -1
        rotate arrayptr arrayptr, windowFocus, windowCount
    endif
endfunction

function guiWindowDrawBackground(id as guiId, w, h)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    a2FillBox x, y, x+w, y+h, 0x80000000
endfunction

function guiWindowDraw(id as guiId, w, h, caption$, focused)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    a2SetLineAA 0
    if focused
        a2FillBox x, y, x+w, y+25, 0xFFFFFF80, 0xFFFFFF80, 0xFFFFC040, 0xFFFFC040
        a2Box x+1, y+25, x+w-2, y+h-2, 0xFFFFC040
        a2Box x+2, y+25, x+w-3, y+h-3, 0xFFFFC040
    else
        a2FillBox x, y, x+w, y+25, 0xFFC0C0C0, 0xFFC0C0C0, 0xFF808080, 0xFF808080
        a2Box x+1, y+25, x+w-2, y+h-2, 0xFF808080
        a2Box x+2, y+25, x+w-3, y+h-3, 0xFF808080
    endif
    
    a2Box x, y, x+w-1, y+h-1, 0xFF000000
    a2Box x+3, y+25, x+w-4, y+h-4, 0xFF000000
    a2SetLineAA 1

    `a2Text gui.fontId, x+6, y+4, caption$, 0xFF000000
    guiFastText(gui.fontId, x+6, y+4, caption$, 0xFF000000)

endfunction
