
function guiRadioButton(id as guiId, x, y, w, h, caption$, checkedId as guiId)
    guiBeginControl(x, y, w, h, 1)
    
    checkedId = guiRadioButtonLogic(id, w, h, checkedId)
    guiRadioButtonDraw(id, w, h, caption$, checkedId)
    
    guiEndControl()
endfunction checkedId

function guiRadioButtonLogic(id as guiId, w, h, checkedId as guiId)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    focused = guiFocus(id, 1)
    
    boxx = x+5
    boxy = y+h/2-10
    
    ` If mouse over, try becoming hot
    if guiInClip(gui.msx, gui.msy)
        if guiInEllipse(gui.msx, gui.msy, boxx, boxy, boxx+20, boxy+20)
            guiSetHot(id)
        else
            if guiInRect(gui.msx, gui.msy, x+25, y, x+w, y+h)
                guiSetHot(id)
            endif
        endif
    endif
    
    if gui.msd = 1
        if guiGetHot() = id
            checkedId = id
            guiSetFocus(id)
        endif
    endif
    
    if focused
        if guiProcessKeyUp(id, VK_SPACE) then checkedId = id
    endif
    
endfunction checkedId

function guiRadioButtonDraw(id as guiId, w, h, caption$, checkedId as guiId)
    x = guiAdjustX(0)
    y = guiAdjustY(0)
    
    focused = guiGetFocus() = id
    
    boxx = x+5
    boxy = y+h/2-10
    
    if (guiGetHot() = id) or (focused and spacekey())
        a2FillCircle boxx+10, boxy+10, 10, 0xFFFFC040
    else
        a2FillCircle boxx+10, boxy+10, 10, 0xFFC0C0C0
    endif
    
    if checkedId = id
        a2FillCircle boxx+10, boxy+10, 5, 0xFF000000
        a2Circle boxx+10, boxy+10, 5, 0xFF000000
    endif
    
    a2Circle boxx+10, boxy+10, 10, 0xFF000000
    
    a2BoxText gui.fontId, x+30, y, x+w, y+h, caption$, 0, 1, 1, 0xFFFFFFFF
    
    if focused
        guiDrawFocusRect(x, y, x+w-1, y+h-1, 0x80FFFFFF)
    endif
endfunction
